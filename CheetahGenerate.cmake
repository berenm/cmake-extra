if(__CHEETAH_GENERATE_CMAKE__)
  return()
endif()
set(__CHEETAH_GENERATE_CMAKE__ TRUE)


find_program(CHEETAH_COMMAND cheetah)
if(NOT CHEETAH_COMMAND)
  message(FATAL_ERROR "Unable to find cheetah, please install python-cheetah.")
  return()
endif()

function(cheetah_generate CHEETAH_TARGET CHEETAH_SOURCE)
  set(CHEETAH_ARGS_FLAGS)
  set(CHEETAH_ARGS_ONE_VALUE   OUTPUT_VARIABLE)
  set(CHEETAH_ARGS_MULTI_VALUE ENVIRONMENT DEPENDS)
  cmake_parse_arguments(CHEETAH "${CHEETAH_ARGS_FLAGS}" "${CHEETAH_ARGS_ONE_VALUE}" "${CHEETAH_ARGS_MULTI_VALUE}" ${ARGN})

  execute_process(
    COMMAND grep "cheetah" ${CHEETAH_SOURCE}
    COMMAND head -1
    COMMAND sed -re "s/^.*cheetah //g"
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    OUTPUT_VARIABLE CHEETAH_OPTIONS
    OUTPUT_STRIP_TRAILING_WHITESPACE
  )
  string(REGEX REPLACE "(--[^ ]+) " "\\1;" CHEETAH_OPTIONS "${CHEETAH_OPTIONS}")

  if(IS_ABSOLUTE ${CHEETAH_SOURCE})
    file(RELATIVE_PATH CHEETAH_OUTPUT "${CMAKE_CURRENT_SOURCE_DIR}" "${CHEETAH_SOURCE}")
  endif()
  string(REPLACE "../" "__/" CHEETAH_OUTPUT "${CHEETAH_SOURCE}")

  string(REPLACE ".tcpp" ".cpp"  CHEETAH_OUTPUT ${CHEETAH_OUTPUT})
  string(REPLACE ".thpp" ".hpp"  CHEETAH_OUTPUT ${CHEETAH_OUTPUT})
  string(REPLACE ".tmpl" ".html" CHEETAH_OUTPUT ${CHEETAH_OUTPUT})
  string(REPLACE "__/" "../" CHEETAH_SHORT_OUTPUT ${CHEETAH_OUTPUT})

  set(CHEETAH_OUTPUT "${CMAKE_CURRENT_BINARY_DIR}/${CHEETAH_OUTPUT}")
  get_filename_component(CHEETAH_OUTPUT_DIR "${CHEETAH_OUTPUT}" PATH)

  file(MAKE_DIRECTORY "${CHEETAH_OUTPUT_DIR}")
  file(WRITE  "${CHEETAH_OUTPUT}.cmake" "")
  file(APPEND "${CHEETAH_OUTPUT}.cmake" "set(CHEETAH_ENVIRONMENT       \"${CHEETAH_ENVIRONMENT}\")\n")
  file(APPEND "${CHEETAH_OUTPUT}.cmake" "set(CHEETAH_COMMAND           \"${CHEETAH_COMMAND}\")\n")
  file(APPEND "${CHEETAH_OUTPUT}.cmake" "set(CHEETAH_WORKING_DIRECTORY \"${CMAKE_CURRENT_SOURCE_DIR}\")\n")
  file(APPEND "${CHEETAH_OUTPUT}.cmake" "set(CHEETAH_OUTPUT_DIRECTORY  \"${CMAKE_CURRENT_BINARY_DIR}\")\n")
  file(APPEND "${CHEETAH_OUTPUT}.cmake" "set(CHEETAH_OPTIONS           \"${CHEETAH_OPTIONS}\")\n")
  file(APPEND "${CHEETAH_OUTPUT}.cmake" "set(CHEETAH_SOURCE            \"${CHEETAH_SOURCE}\")\n")

  add_custom_command(OUTPUT ${CHEETAH_OUTPUT}
    COMMAND ${CMAKE_COMMAND} -DCHEETAH_OUTPUT_VAR_FILE="${CHEETAH_OUTPUT}.cmake" -P ${CMAKE_EXTRA_LIST_DIR}/RunCheetah.cmake
    DEPENDS ${CHEETAH_SOURCE} ${CHEETAH_DEPENDS}
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    COMMENT "Generating ${CHEETAH_SHORT_OUTPUT}..."
  )
  add_custom_target(${CHEETAH_TARGET}
    DEPENDS ${CHEETAH_OUTPUT}
  )

  if(CHEETAH_OUTPUT_VARIABLE)
    set(${CHEETAH_OUTPUT_VARIABLE} ${CHEETAH_OUTPUT} PARENT_SCOPE)
  endif()
endfunction()
