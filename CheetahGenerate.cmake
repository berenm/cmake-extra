if(__CHEETAH_GENERATE_CMAKE__)
  return()
endif()
set(__CHEETAH_GENERATE_CMAKE__ TRUE)


find_program(CHEETAH_COMMAND cheetah)
if(NOT CHEETAH_COMMAND)
  message(FATAL_ERROR "Unable to find cheetah, please install python-cheetah.")
  return()
endif()

function(cheetah_generate CHEETAH_TARGET CHEETAH_SOURCE)
  set(CHEETAH_ARGS_FLAGS)
  set(CHEETAH_ARGS_ONE_VALUE   WORKING_DIRECTORY OUTPUT_VARIABLE)
  set(CHEETAH_ARGS_MULTI_VALUE ENVIRONMENT DEPENDS)
  cmake_parse_arguments(CHEETAH "${CHEETAH_ARGS_FLAGS}" "${CHEETAH_ARGS_ONE_VALUE}" "${CHEETAH_ARGS_MULTI_VALUE}" ${ARGN})

  if(NOT CHEETAH_WORKING_DIRECTORY)
    set(CHEETAH_WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR})
  endif()

  execute_process(
    COMMAND grep "cheetah" ${CMAKE_CURRENT_SOURCE_DIR}/${CHEETAH_SOURCE}
    COMMAND head -1
    COMMAND sed -re "s/^.*cheetah //g"
    OUTPUT_VARIABLE CHEETAH_OPTIONS
    OUTPUT_STRIP_TRAILING_WHITESPACE
  )
  string(REGEX REPLACE "(--[^ ]+) " "\\1;" CHEETAH_OPTIONS "${CHEETAH_OPTIONS}")

  if(NOT IS_ABSOLUTE ${CHEETAH_SOURCE})
    set(CHEETAH_SOURCE ${CMAKE_CURRENT_SOURCE_DIR}/${CHEETAH_SOURCE})
  endif()

  string(REPLACE ${CMAKE_CURRENT_SOURCE_DIR} ${CHEETAH_WORKING_DIRECTORY} CHEETAH_OUTPUT ${CHEETAH_SOURCE})
  string(REPLACE ".tcpp" ".cpp"  CHEETAH_OUTPUT ${CHEETAH_OUTPUT})
  string(REPLACE ".thpp" ".hpp"  CHEETAH_OUTPUT ${CHEETAH_OUTPUT})
  string(REPLACE ".tmpl" ".html" CHEETAH_OUTPUT ${CHEETAH_OUTPUT})
  string(REPLACE ${CMAKE_CURRENT_BINARY_DIR}/ "" CHEETAH_SHORT_OUTPUT ${CHEETAH_OUTPUT})

  add_custom_command(OUTPUT ${CHEETAH_OUTPUT}
    COMMAND ${CMAKE_COMMAND} -DCHEETAH_ENVIRONMENT="${CHEETAH_ENVIRONMENT}"
                             -DCHEETAH_COMMAND="${CHEETAH_COMMAND}"
                             -DCHEETAH_WORKING_DIRECTORY="${CHEETAH_WORKING_DIRECTORY}"
                             -DCHEETAH_OPTIONS="${CHEETAH_OPTIONS}"
                             -DCHEETAH_SOURCE="${CHEETAH_SOURCE}"
                             -P ${CMAKE_CURRENT_LIST_DIR}/RunCheetah.cmake
    VERBATIM
    DEPENDS ${CHEETAH_SOURCE} ${CHEETAH_DEPENDS}
    WORKING_DIRECTORY ${CHEETAH_WORKING_DIRECTORY}
    COMMENT "Generating ${CHEETAH_SHORT_OUTPUT}..."
  )
  add_custom_target(${CHEETAH_TARGET}
    DEPENDS ${CHEETAH_OUTPUT}
  )

  if(CHEETAH_OUTPUT_VARIABLE)
    set(${CHEETAH_OUTPUT_VARIABLE} ${CHEETAH_OUTPUT} PARENT_SCOPE)
  endif()
endfunction()
